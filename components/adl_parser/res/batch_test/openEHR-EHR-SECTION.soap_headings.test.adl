archetype 
	openEHR-EHR-SECTION.soap_headings.draft
concept 
	[at0001]	-- problem/SOAP headings

description
	author = 		<"Sam Heard <s.heard@littlerock.com>">
	submission = 	<
		organisation = <"World Health Organisation">
		date = <2003-06-10>
	>
	version = 		<"draft">
	status = 		<"draft">
	revision = 		<"1.1">
	description("en") =	<
		purpose = 		<"SOAP Heading structure for the problem-oriented medical record (POMR)">
		use = 		<"">
		misuse = 		<"">
	>
	adl_version =	<"1.2">
	rights = 		<"">

definition
	SECTION[at0000] matches {						-- problem
		name matches {
			CODED_TEXT matches {
				code matches {[ac0001]}
			}
		}
		items cardinality matches {0..*} matches {
			SECTION[at1000] occurrences matches {0..1}  matches {	-- subjective
				name matches {
					CODED_TEXT matches {
						code matches {[ac0002]}
					}
				}
				items cardinality matches {0..*} matches {
					allow_archetype OBSERVATION occurrences matches {0..*} matches {		-- any ENTRY 
						include
							id matches {/openehr-ehr-observation.*.*/}
					}

					allow_archetype SECTION occurrences matches {0..*} matches {	-- any archetype SECTION structure
						include
							id matches {/openehr-ehr-section.*.*/}
					} 		
				}
			}

			SECTION[at2000] occurrences matches {0..1}  matches {	-- objective
				name matches {
					CODED_TEXT matches {
						code matches {[ac0003]}
					}
				}
				items cardinality matches {0..*} matches {
					allow_archetype OBSERVATION occurrences matches {0..*} matches {
						include
							id matches {/openehr-ehr-observation.*.*/}
					}
					allow_archetype SECTION occurrences matches {0..*} matches {
						include
							id matches {/openehr-ehr-section.*.*/}
					}
				}
			}

			SECTION[at3000] occurrences matches {0..1}  matches {		-- assessment
				name matches {
					CODED_TEXT matches {
						code matches {[ac0004]}
					}
				}
				items cardinality matches {0..*} matches {
					allow_archetype EVALUATION occurrences matches {0..*} matches {
						include
							id matches {/openehr-ehr-evaluation.*.*/}
					}
					allow_archetype SECTION occurrences matches {0..*} matches {
						include
							id matches {/openehr-ehr-section.*.*/}
					}
				}
			}

			SECTION[at4000] occurrences matches {0..1}  matches {	-- plan
				name matches {
					CODED_TEXT matches {
						code matches {[ac0005]}
					}
				}
				items cardinality matches {0..*} matches {
					allow_archetype EVALUATION occurrences matches {0..*} matches {
						include
							id matches {/openehr-ehr-evaluation.*.*/}
					}
					allow_archetype SECTION occurrences matches {0..*} matches {
						include
							id matches {/openehr-ehr-section.*.*/}
					}
				}
			}
		}
	}


ontology 
	primary_language = <"en">
	languages_available = <"en", "de">
	terminologies_available = <"snomed_ct", ...>
            
	term_definitions("en") = <
		items("at0000") = <text = <"problem">; 			description = <"The problem experienced by the subject of care to which the contained information relates">>
		items("at0001") = <text = <"problem/SOAP headings">; 	description = <"SOAP heading structure for multiple problems">>
		items("at1000") = <text = <"subjective">; 		description = <"That part of the information that was provided by the subject of care (or another reporter) to the clinician">>
		items("at2000") = <text = <"objective">; 			description = <"That part of the information that was provided by the clinician">>
		items("at3000") = <text = <"assessment">; 		description = <"The clinician's professional assessment of the problem">>
		items("at4000") = <text = <"plan">; 			description = <"The clinician's professional advice">>
		items("at4001") = <text = <"education">; 			description = <"xxxxx">>
	>

	term_definitions("de") = <
		translation = <"hpotter@chimera.ucl.ac.uk">
		items("at0000") = <text = <"klinisches Problem">; 	description = <"Das Problem des Patienten worauf sich diese Informationen beziehen">>
		items("at0001") = <text = <"Problem/SOAP Schema">; 	description = <"SOAP-Schlagwort-Gruppierungsschema fuer mehrfache Probleme">>
		items("at1000") = <text = <"subjektives">; 		description = <"Jene Informationen, die vom Patienten oder von anderer patientennahe Quelle stammen">>
		items("at2000") = <text = <"objektives">; 		description = <"Jene Informationen, die vom Pflegenden stammen">>
		items("at3000") = <text = <"Auswertung">; 		description = <"Klinisch-professionelle Auswertung des Problems">>
		items("at4000") = <text = <"Plan">; 			description = <"Klinisch-professionelle Beratung des Pflegenden">>
		items("at4001") = <text = <"*education(en)">; 		description = <"*xxxxx(en)">>
	>

	constraint_definitions("en") = <
		items("ac0001") = <text = <"problem type">; 	description = <"any term that can be a problem, diagnosis or issue of significance to the patient">>
		items("ac0002") = <text = <"subjective">; 	description = <"any term that means information gained from the patient or other parties">>
		items("ac0003") = <text = <"objective">; 		description = <"any term that means information measured by the clinician">>
		items("ac0004") = <text = <"assessment">; 	description = <"any term that means an assessment by the clinician">>
		items("ac0005") = <text = <"plan">; 		description = <"any term that means actions proposed by the clinician">>
	>

	constraint_definitions("de") = <
		items("ac0001") = <text = <"*problem type(en)">; 	description = <"*any term that can be a problem, diagnosis or issue of significance to the patient(en)">>
		items("ac0002") = <text = <"*subjective(en)">; 	description = <"*any term that means information gained from the patient or other parties(en)">>
		items("ac0003") = <text = <"*objective(en)">; 		description = <"*any term that means information measured by the clinician(en)">>
		items("ac0004") = <text = <"*assessment(en)">; 	description = <"*any term that means an assessment by the clinician(en)">>
		items("ac0005") = <text = <"*plan(en)">; 		description = <"*any term that means actions proposed by the clinician(en)">>
	>

	term_binding("snomed_ct") = <
		items("at0000") = <[snomed_ct::234523959]>		-- problem
		items("at1000") = <[snomed_ct::293854954]>		-- subjective
		items("at2000") = <[snomed_ct::293854955]>		-- objective
		items("at3000") = <[snomed_ct::293854958]>		-- assessment
		items("at4000") = <[snomed_ct::293854959]>		-- plan
	>

	constraint_binding("snomed_ct") = <
		items("ac0001") = <query("terminology", "terminology_id = snomed_ct; has_relation [102002] with_target [128004]")> -- is-a problem type
		items("ac0002") = <query("terminology", "terminology_id = snomed_ct; synonym_of [128025]")> -- subjective
		items("ac0003") = <query("terminology", "terminology_id = snomed_ct; synonym_of [128031]")> -- objective
		items("ac0004") = <query("terminology", "terminology_id = snomed_ct; synonym_of [128090]")> -- assessment
		items("ac0005") = <query("terminology", "terminology_id = snomed_ct; synonym_of [128093]")> -- plan
	>

