archetype
	openEHR-EHR-OBSERVATION.weight.test

concept
	[at0000]	-- Body weight
description
	author = <"Sam Heard <sam.heard@oceaninformatics.biz>">
	submission = <
		organisation = <"openEHR Foundation">
		date = <2004-05-18>
	>
		version = <"version">
	status = <"draft">
	revision = <"1.0">
	description("en") = <
		purpose = <"Describe systemic blood pressure measurement result and protocol">
		use = <"">
		misuse = <"">
	>
	adl_version = <"1.2">
	rights = <"">

definition
	OBSERVATION[at0000] matches {	-- Body weight
		data matches {
			HISTORY[at0002] matches {	-- history
				events cardinality matches {1..*} matches {
					EVENT[at0003] occurrences matches {0..*} matches {	-- Any event
						data matches {
							SIMPLE[at0001] matches {	-- structure
								item matches {
									ELEMENT[at0004] matches {	-- Weight
										value matches {
											REAL_QUANTITY matches {
												property matches {"mass"}
												units cardinality matches {1} matches {
													UNIT matches {
														unit_string matches {"Kg"}
														magnitude matches {|0.0..1000.0|}
													}
												}
											}
										}
									}
								}
							}
						}
					}
					EVENT[at0005] occurrences matches {0..*} matches {	-- Weight loss
						aggregate_math_function matches {"Change"}
						display_as_positive matches {True}
						data matches {
							use_node SIMPLE /[at0000]/data[at0002]/events[at0003]/data[at0001]/
						}
					}
					EVENT[at0007] occurrences matches {0..*} matches {	-- Weight gain
						aggregate_math_function matches {"Change"}
						data matches {
							use_node SIMPLE /[at0000]/data[at0002]/events[at0003]/data[at0001]/
						}
					}
				}
			}
		}
		state matches {
			LIST[at0008] matches {	-- state structure
				items cardinality matches {0..1} matches {
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Clothing
						value matches {
							CODED_TEXT matches {
								code matches {
									[local::
									at0010, 	-- Fully dressed
									at0011, 	-- Lightly dressed
									at0012, 	-- Underclothes
									at0013]	-- Naked
								}
								assumed_value matches {"at0011"}
							}
						}
					}
				}
			}
		}
		protocol matches {
			LIST[at0015] matches {	-- protocol structure
				items cardinality matches {0..1} matches {
					ELEMENT[at0016] occurrences matches {0..1} matches {	-- Device
						value matches {
							TEXT matches {*}
						}
					}
				}
			}
		}
	}

ontology
	primary_language = <"en">
	languages_available = <"en", ...>
	term_definitions("en") = <
		items("at0005") = <
			text = <"Weight loss">
			description = <"Amount of weight lost over the period">
		>
		items("at0007") = <
			text = <"Weight gain">
			description = <"Amount of weight gained over the period">
		>
		items("at0008") = <
			text = <"state structure">
			description = <"@ internal @">
		>
		items("at0009") = <
			text = <"Clothing">
			description = <"The amount of clothes worn at the time of weighing">
		>
		items("at0012") = <
			text = <"Underclothes">
			description = <"Underclothes/gown only">
		>
		items("at0001") = <
			text = <"structure">
			description = <"@ internal @">
		>
		items("at0004") = <
			text = <"Weight">
			description = <"The weight of the whole body">
		>
		items("at0011") = <
			text = <"Lightly dressed">
			description = <"No shoes and lightly dressed">
		>
		items("at0010") = <
			text = <"Fully dressed">
			description = <"Shoes and normal clothing">
		>
		items("at0013") = <
			text = <"Naked">
			description = <"Without clothes">
		>
		items("at0000") = <
			text = <"Body weight">
			description = <"Total body weight - a surrogate for naked body weight">
		>
		items("at0015") = <
			text = <"protocol structure">
			description = <"@ internal @">
		>
		items("at0002") = <
			text = <"history">
			description = <"@ internal @">
		>
		items("at0003") = <
			text = <"Any event">
			description = <"Any event - based on offset from the baseline">
		>
		items("at0016") = <
			text = <"Device">
			description = <"The name of the weighing device">
		>
	>
	constraint_definitions("en") = <
	>
