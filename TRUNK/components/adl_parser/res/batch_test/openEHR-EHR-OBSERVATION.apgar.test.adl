archetype
	openEHR-EHR-OBSERVATION.apgar.test

concept
	[at0000]	-- Apgar score
description
	author = <"Sam Heard <sam.heard@oceaninformatics.biz>">
	submission = <
		organisation = <"openEHR Foundation">
		date = <2004-05-18>
	>
		version = <"version">
	status = <"draft">
	revision = <"1.0">
	description("en") = <
		purpose = <"Describe systemic blood pressure measurement result and protocol">
		use = <"">
		misuse = <"">
	>
	adl_version = <"1.2">
	rights = <"">

definition
	OBSERVATION[at0000] matches {	-- Apgar score
		data matches {
			HISTORY[at0002] matches {	-- history
				events cardinality matches {1..*} matches {
					EVENT[at0003] occurrences matches {0..1} matches {	-- 1 minute
						offset matches {P1m}
						data matches {
							LIST[at0001] matches {	-- structure
								items cardinality matches {0..1} matches {
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Heart rate
										runtime_label matches {
											CODED_TEXT matches {
												code matches {[ac0001]}		-- =Heart rate
											}
										}
										value matches {
											ORDINAL matches {
												value matches {
													0|[local::at0006], 	-- No heart beat
													1|[local::at0007], 	-- Less than 100 beats per minute
													2|[local::at0008]  	-- Greater than 100 beats per minute
												}
											}
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Breathing
										value matches {
											ORDINAL matches {
												value matches {
													0|[local::at0010], 	-- No effort
													1|[local::at0011], 	-- Moderate effort
													2|[local::at0012]  	-- Crying
												}
											}
										}
									}
									ELEMENT[at0013] occurrences matches {0..1} matches {	-- Muscle tone
										value matches {
											ORDINAL matches {
												value matches {
													0|[local::at0014], 	-- Limp
													1|[local::at0015], 	-- Some flexing of arms and legs
													2|[local::at0016]  	-- Active motion
												}
											}
										}
									}
									ELEMENT[at0017] occurrences matches {0..1} matches {	-- Reflex response
										value matches {
											ORDINAL matches {
												value matches {
													0|[local::at0018], 	-- No response to airways suction
													1|[local::at0019], 	-- Grimace during airways suction
													2|[local::at0020]  	-- Grimace and cough/sneeze during airways suction
												}
											}
										}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {	-- Colour
										value matches {
											ORDINAL matches {
												value matches {
													0|[local::at0022], 	-- Whole body is blue or pale
													1|[local::at0023], 	-- Good colour in body, hands or feet blue 
													2|[local::at0024]  	-- Completely pink or good colour
												}
											}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {	-- Total
										value matches {
											COUNT matches {
												magnitude matches {|0..10|}
											}
										}
									}
								}
							}
						}
					}
					EVENT[at0026] occurrences matches {0..1} matches {	-- 2 minute
						offset matches {P2m}
						data matches {
							use_node LIST /[at0000]/data[at0002]/events[at0003]/data[at0001]/
						}
					}
					EVENT[at0027] occurrences matches {0..1} matches {	-- 5 minute
						offset matches {P5m}
						data matches {
							use_node LIST /[at0000]/data[at0002]/events[at0003]/data[at0001]/
						}
					}
					EVENT[at0028] occurrences matches {0..*} matches {	-- 10 minute
						offset matches {P10m}
						data matches {
							use_node LIST /[at0000]/data[at0002]/events[at0003]/data[at0001]/
						}
					}
				}
			}
		}
	}

ontology
	primary_language = <"en">
	languages_available = <"en", ...>
	terminologies_available = <"LNC205", ...>
	term_definitions("en") = <
		items("at0011") = <
			text = <"Moderate effort">
			description = <"*">
		>
		items("at0010") = <
			text = <"No effort">
			description = <"*">
		>
		items("at0013") = <
			text = <"Muscle tone">
			description = <"Assessment of the baby's muscle tone">
		>
		items("at0012") = <
			text = <"Crying">
			description = <"*">
		>
		items("at0015") = <
			text = <"Some flexing of arms and legs">
			description = <"*">
		>
		items("at0014") = <
			text = <"Limp">
			description = <"*">
		>
		items("at0017") = <
			text = <"Reflex response">
			description = <"Assessment of the response of the baby to suction from a nostril">
		>
		items("at0016") = <
			text = <"Active motion">
			description = <"*">
		>
		items("at0019") = <
			text = <"Grimace during airways suction">
			description = <"*">
		>
		items("at0018") = <
			text = <"No response to airways suction">
			description = <"*">
		>
		items("at0022") = <
			text = <"Whole body is blue or pale">
			description = <"*">
		>
		items("at0023") = <
			text = <"Good colour in body, hands or feet blue ">
			description = <"*">
		>
		items("at0020") = <
			text = <"Grimace and cough/sneeze during airways suction">
			description = <"*">
		>
		items("at0021") = <
			text = <"Colour">
			description = <"The colour of the skin on the body and trunk and limbs">
		>
		items("at0026") = <
			text = <"2 minute">
			description = <"Apgar score 2 minutes after birth">
		>
		items("at0027") = <
			text = <"5 minute">
			description = <"Apgar score 5 minutes after birth">
		>
		items("at0024") = <
			text = <"Completely pink or good colour">
			description = <"*">
		>
		items("at0000") = <
			text = <"Apgar score">
			description = <"Clinical score derived from assessment of breathing, colour, muscle tone, heart rate and reflex response usually taken at 1, 5 and 10 minutes after birth">
		>
		items("at0001") = <
			text = <"structure">
			description = <"@ internal @">
		>
		items("at0002") = <
			text = <"history">
			description = <"@ internal @">
		>
		items("at0003") = <
			text = <"1 minute">
			description = <"Apgar score at one minute">
		>
		items("at0004") = <
			text = <"New element">
			description = <"*">
		>
		items("at0005") = <
			text = <"Heart rate">
			description = <"Assessment of heart function in the new born">
		>
		items("at0006") = <
			text = <"No heart beat">
			description = <"*">
		>
		items("at0007") = <
			text = <"Less than 100 beats per minute">
			description = <"*">
		>
		items("at0008") = <
			text = <"Greater than 100 beats per minute">
			description = <"*">
		>
		items("at0009") = <
			text = <"Breathing">
			description = <"Assessment of the neonate's breathing effort">
		>
		items("at0025") = <
			text = <"Total">
			description = <"The total of the ordinal scores for each parameter">
		>
		items("at0028") = <
			text = <"10 minute">
			description = <"Apgar score 10 minutes after birth">
		>
	>
	constraint_definitions("en") = <
		items("ac0001") = <
			text = <"=Heart rate">
			description = <"*">
		>
	>
--
--  adding "/" to the beginning of path causes parsing failure
--
--	term_binding("LNC205") = <
--		items("/[at0000]/data[at0002]/events[at0028]/data/items[at0025]/") = <[LNC205::9271-8]>
--		items("/[at0000]/data[at0002]/events[at0003]/data[at0001]/items[at0025]/") = <[LNC205::9272-6]>
--	>
